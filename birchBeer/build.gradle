apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdk compileSdkVer
    ndkVersion ndkVersionVar

    defaultConfig {
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 1
        versionName "0.0.2"
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

        archivesBaseName = "birchbeer-lib-$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    namespace 'com.birchbeer'
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.6.0'
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = 'com.birchbeer'
                artifactId = 'birchbeer-lib'
                version = '0.0.2'

                artifact("$buildDir/outputs/aar/birchbeer-lib-0.0.2-release.aar") {
                    builtBy tasks.named("bundleReleaseAar")
                }
            }
        }
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/gtomek/birchbeer")
                credentials {
                    username =  System.getenv("GITHUB_API_USER") ?: project.findProperty("gpr.username")
                    password =  System.getenv("GITHUB_API_TOKEN_DEPLOY") ?: project.findProperty("gpr.token")
                    println "GitHubPackages build.gradle\n\tusername=$username\n\ttoken=$password"
                }
            }
        }
    }
}

apply from: '../gradle-mvn-push.gradle'

